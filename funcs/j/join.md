# join

## 语法

`join(X,Y)` 或 `X<-Y`

## 参数

**X** 和 **Y** 可以是标量、向量、元组、矩阵或表。

## 详情

合并 *X* 和 *Y*。

## 例子

如果 *X* 是标量，*Y* 可以是标量或向量，结果是一个向量；*Y*
可以是元组，结果是一个元组

```
1 <- 3;
// output
[1,3]

4<-1 2 3;
// output
[4,1,2,3]

1 <- (2,"A")
// 配置参数 appendTupleAsAWhole=true 时
// output
(1,(2,"A"))
// 配置参数 appendTupleAsAWhole=false 时
// output
(1,2,"A")
```

如果 *X* 是向量，*Y* 可以是标量、向量或元素类型与 X 相同的元组。它将产生一个更长的向量。

```
[1,2,3] <- 4;
// output
[1,2,3,4]

[1,2,3] <- [4,5,6];
// output
[1,2,3,4,5,6]
```

如果 X
是元组，Y可以是标量、向量或元组。它将产生一个更长的元组。

```
x = (1,"A")
y = 2
x <- y
(1,"A",2)

y = [2,3]
(1,"A",[2,3])

y = (2,"B")
// 配置参数 appendTupleAsAWhole=true 时
// output
(1,"A",(2,"B"))
// 配置参数 appendTupleAsAWhole=false 时
// output
(1,"A",2,"B")
```

如果 *X* 是矩阵，*Y* 必须是和 *X* 具有相同行数的向量或矩阵。结果是一个和
*X* 具有相同行数的矩阵。

```
1..6$2:3 <- [7,8];
```

| #0 | #1 | #2 | #3 |
| --- | --- | --- | --- |
| 1 | 3 | 5 | 7 |
| 2 | 4 | 6 | 8 |

```
(1..6$2:3) <- (7..12$2:3);
```

| #0 | #1 | #2 | #3 | #4 | #5 |
| --- | --- | --- | --- | --- | --- |
| 1 | 3 | 5 | 7 | 9 | 11 |
| 2 | 4 | 6 | 8 | 10 | 12 |

如果 *X* 是一个表，*Y* 必须是一个和 *X* 具有相同行数的向量或表。结果是一个和
*X* 具有相同行数的表。

```
a=table(1..3 as x, 4.5 6.7 8.5 as y);
a;
```

| x | y |
| --- | --- |
| 1 | 4.5 |
| 2 | 6.7 |
| 3 | 8.5 |

```
b=table(700 500 800 as z);
b
```

| z |
| --- |
| 700 |
| 500 |
| 800 |

```
c=join(a,b);
c;
```

| x | y | z |
| --- | --- | --- |
| 1 | 4.5 | 700 |
| 2 | 6.7 | 500 |
| 3 | 8.5 | 800 |

```
a=table(1..3 as x, `IBM`C`AAPL as y);
b=table(172.3 25 106.5 as z);
c=a<-b;
c
```

| x | y | z |
| --- | --- | --- |
| 1 | IBM | 172.3 |
| 2 | C | 25 |
| 3 | AAPL | 106.5 |

相关函数：[cj (cross\_join)](../c/cj.md)

